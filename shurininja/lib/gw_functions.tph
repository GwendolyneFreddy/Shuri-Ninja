/* ================================================================================================== *
 *  FUNCTION GW_CHECK_LEATHER : CHECKS if a given itm file is a Studded leather or a Hide armor.      *
 * -------------------------------------------------------------------------------------------------- *
 *  Returns GW_unusable = 1, used in main_component.tpa to add an unusability flag for Shuri Ninjas.  *
 * ================================================================================================== */

DEFINE_PATCH_FUNCTION ~GW_CHECK_LEATHER~
	RET	GW_unusable
BEGIN

	SET	GW_unusable = 0
	SET	GW_Studded	= 0	 // 1 = Studded leather armor if opcode 0 & paramètre2 = 8 (Slashing Weapons) present in global effects list.
	SET	GW_Hide	= 0      // 1 = Hide armor if Min Strength >= 6 (not very elegant, but it works!).

	READ_LONG  0x6a fx_off        // Feature Block Table offset
	READ_SHORT 0x6e eq_eff_index  // Casting Feature Block offset	- Index into equipping feature blocks
	READ_SHORT 0x70 eq_eff_count  // Casting Feature Block count
	SET GW_fx_type = 0

	// Looping through global effects
	FOR (i = 0 ; i < eq_eff_count ; ++i) BEGIN

		PATCH_IF (GW_Studded = 0) AND (GW_Hide = 0) BEGIN

			READ_SHORT (fx_off + (0x08 * GW_fx_type) + ((eq_eff_index + i * (0x30 + (0xd8 * GW_fx_type))))) opcode ELSE 0

			// If Opcode Stat: AC vs. Damage Type Modifier [0]
			PATCH_IF (opcode = 0) BEGIN

				READ_LONG (fx_off + 0x08 + (0x10 * GW_fx_type) + ((eq_eff_index + i * (0x30 + (0xd8 * GW_fx_type))))) parameter2

				// (parameter2 = 8) : Slashing Weapons specific to Studded leather armors.
				PATCH_IF (parameter2 = 8) BEGIN
					SET GW_Studded = 1
					SET GW_unusable = 1
					SET i = eq_eff_count
				END	// of PATCH_IF (parameter2 = 8)

			END		// of PATCH_IF (opcode = 0)

		END			// of PATCH_IF (GW_Studded = 0) AND (GW_Hide = 0) 

	END				// of looping through global effects

	// It is faster to check this first, but safer to proceed it after Studded Leather Armor checking
	PATCH_IF (GW_Hide = 0) AND (GW_unusable = 0) BEGIN

		// If Minimum strength >=6 (Leather <= 4)
		PATCH_IF (SHORT_AT 0x26 >= 6) BEGIN
			SET GW_Hide = 1
			SET GW_unusable = 1
		END // of PATCH_IF (SHORT_AT 0x26 >= 6)

	END     // of PATCH_IF (GW_Hide = 0) AND (GW_unusable = 0)

END         // of DEFINE_PATCH_FUNCTION
