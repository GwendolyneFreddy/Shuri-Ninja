/* ============================= *
 *              Kit              *
 * ============================= */

ADD_KIT ~SHURI~
// CLASS WEAPON PROFICIENCIES____________________________________________________________________________________
// CLASWEAP			SMALL_SWORD	LARGE_SWORD		BLUNT       MISSILE     BOW         SPIKED      AXE         SPEAR
~SHURI                   0           1           0           1           1           0           0           0~		// Types d'armes autorisés
// WEAPON PROFICIENCIES______________________________________________________________________________________________________
//                                      B       T   S           F   Q             S S
//                                      A       W   C           L   U             W I T
//                      L S             S   S   O   I           A   A           T O N W
//                      A M             T L H   H   M   W       I   R           W R G O
//                      R A             A O O   A   I   A       L   T C   S     O D L -
//                      G L           M R N R   N   T   R     H M   E R L H     - & E W E E E E E E E E E E E E E E E E E E E
//                      E L       S   I D G T   D K A D H     A O   R O O O     H S W E X X X X X X X X X X X X X X X X X X X
//                      S S   S B P   S S S S   E A R A A   S L R   S S N R   S A H E A T T T T T T T T T T T T T T T T T T T
//                      W W   P L I   S W W W   D T W G M C P B N M T S G T D L N I A P R R R R R R R R R R R R R R R R R R R
//                      O O B E U K A I O O O A S A A G M L E E I A A B B B A I D E P O A A A A A A A A A A A A A A A A A A A
//                      R R O A N E X L R R R X W N K E E U A R N C F O O O R N E L O N 2 3 4 5 6 7 8 9 1 1 1 1 1 1 1 1 1 1 2
// WEAPPROF.2DA         D D W R T D E E D D D E O A I R R B R D G E F W W W T G D D N S                 0 1 2 3 4 5 6 7 8 9 0
~SHURI                  0 0 2 0 0 0 0 2 0 0 0 0 0 2 2 2 0 0 0 0 0 0 0 0 0 2 2 0 0 0 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~	// Compétences autorisées
// LARGE_SWORD = 2 - BOW = 2 - MISSILE = 2 - KATANA = 2 - SCIMITARWAKISASHININJATO = 2 - DAGGER = 2 - SHORTBOW = 2 - DART = 2 - SINGLE-WEAPON = 2 - TWO-WEAPONS = 3
// MINIMUM KIT STATS_______________________________________________
// ABCLASRQ.2DA         STR      DEX    CON     INT     WIS     CHR
~SHURI                   8       12      8       10      10      8~							// Scores de caractéristiques minimaux nécessaires.
// KIT STAT MODIFIERS_______________________________________________
// ABCLSMOD.2DA         STR      DEX    CON     INT     WIS     CHR
~SHURI                   -2      2       -2      0       0       -2~						// Modificateurs des scores de caractéristiques.
// REQUIREMENTS TO DUAL TO THIS KIT________________________________
// ABDCDSRQ.2DA         STR      DEX    CON     INT     WIS     CHR
~SHURI                   0       0       0       0       0       0~							// Caractéristiques minimales pour se jumeler VERS le kit.
// REQUIREMENTS TO DUAL FROM THIS KIT______________________________
// ABDCSCRQ.2DA         STR      DEX    CON     INT     WIS     CHR
~SHURI                   0       0       0       0       0       0~							// Caractéristiques minimales pour se jumeler DEPUIS le kit.
// ALIGNMENT RESTRICTIONS_________________________________________________________________
// ALIGNMNT.2DA         LG      LN      LE      NG      NN      NE      CG      CN      CE
~SHURI                   0       0       0       0       0       0       1       1       1~	// Alignements autorisés.
// DUAL CLASS OPTIONS______________________________________________
// DUALCLASS.2DA        FIG     CLE     MAG     THI     DRU     RAN
~SHURI                   0       0       0       0       0       0~							// Possibilités de jumelage.
// KIT ABILITIES 2DA FILE (path to CLAB-type file)
~%MOD_FOLDER%/2da/gwshurin.2da~																// Habilités gagnées par niveau.
// RACIAL KIT AVAILABILITY___________________________________________________________
// KITTABLE.2DA    P = Paladin - H = Humain - E = Elf - HE = Half-Elf - HL = Halfling
~K_T_H~																						// Races et classes acceptant le kit.
// UNUSABLE FLAGS AND KIT BASE CLASS
// CLASSES: Mage = 1, Fighter = 2, Cleric = 3, Thief = 4, Bard = 5, Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19, Monk = 20
// KITLIST.2DA
~0x00010000     4~	// Autorisation d'objets - 0x00010000 = STALKER
// HIGH LEVEL ABILITIES ABBREVIATION
// LUABBR.2DA
~Fi0~																						// Habiletés de Haut niveau.
// TOB STARTING EQUIPMENT_________________________________________________________________________________________________________________________________________________
//                                                                                   B                                                                                   W
//                          S                                    C            A      R                                                                                   E
//                 A        H                     R      R       L     B      M      A              A         A         A         M        M        M       M     M      A
//                 R        I        H            I      I       O     O      U      C      B       M         M         M         I        I        I       I     I      P
//                 M        E        E      B     N      N       A     O      L      E      E       M         M         M         S        S        S       S     S      O
//                 O        L        L      A     G      G       C     T      E      R      L       O         O         O         C        C        C       C     C      N
// 25STWEAP.2DA    R        D        M      G     1      2       K     S      T      S      T       1         2         3         1        2        3       4     5      1
				~LEAT20  DART03,40 HELM07 BAG26 RING06 RING31 CLCK27 BOOT01 AMUL19 BRAC10 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW11 BOW19 XBOW06~

// CHARACTER CREATION KIT DESCRIPTION
// KITLIST.2DA
SAY @101				// LOWER = Nom du kit en minuscules
SAY @102				// MIXED = Nom du kit en minuscules avec majuscule au début
SAY (AT GW_shuri_desc)	// HELP  = Description du kit

// EE specific

LAF fl#add_kit_ee
	INT_VAR
		//CLASTEXT.2DA       BIOGRAPHY
//	    biography     = ~~
		//CLASTEXT.2DA       BRIEFDESC
//	    briefdesc     = ~kit_briefdesc~
		//CLASTEXT.2DA       FALLEN
//	    fallen        = ~0~
		//CLASTEXT.2DA       FALLEN_NOTICE
//	    fallen_notice = ~-1~
	STR_VAR
		//internal kit name
		kit_name	= ~SHURI~
		//BACKSTAB.2DA       0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
//		backstab	= ~~																																	// Inherit from parent class
		//CLSWPBON.2DA		GETS_PROF_APR       UNARMED_DIVISOR     ZERO_SKILL_THAC0
//		clswpbon	= ~0                   0                   3~																							// Inherit from parent class
		//NUMWSLOT.2DA		SLOTS
//		numwslot	= ~2~																																	// Inherit from parent class
		//THIEFSKL.2DA		START_POINTS       LEVEL_POINTS
//		thiefskl	= ~~																																	// Inherit from parent class
		//TRAPLIMT.2DA		LIMIT
//		traplimt	= ~~																																	// Inherit from parent class
		//CLASCOLR.2DA METAL     MINOR_CLOTH     MAIN_CLOTH     LEATHER     ARMOR
		clascolr	= ~66        136             136            66          66~
		//CLASISKL.2DA		START_POINTS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS
//		clasiskl	=		~0                0              0              0                 0                   0                   0~					// Inherit from parent class
		//THIEFSCL.2DA       PICK_POCKETS     OPEN_LOCKS     FIND_TRAPS     MOVE_SILENTLY     HIDE_IN_SHADOWS     DETECT_ILLUSION     SET_TRAPS     STEALTH
//		thiefscl	=			~100              100            100            100               100                 100                 0             0~	// Inherit from parent class
		//HPCLASS.2DA		TABLE
//		hpclass		= ~HPROG~																																// Inherit from parent class
		//CLSRCREQ.2DA		HUMAN               ELF                 HALF_ELF            DWARF               HALFLING            GNOME               HALFORC
//		clsrcreq	=		~1                   0                   0                   0                   0                   0                   0~
		//CLASTHAC.2DA		BONUS
//		clasthac	= ~0~																																	// Inherit from parent class
		//SNEAKATT.2DA   0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
//		sneakatt	= ~~
		//CRIPPSTR.2DA 	 0     1     2     3     4     5     6     7     8     9     10     11     12     13     14     15     16     17     18     19     20     21     22     23     24     25     26     27     28     29     30     31     32     33     34     35     36     37     38     39     40     41     42     43     44     45     46     47     48     49     50
//		crippstr	= ~~
END


/* ============================= *
 *            Spells             *
 * ============================= */

// Blinding strike
COPY_EXISTING ~SPCL144.SPL~		~override/gwshur1.spl~ 
	SAY NAME1 @201 SAY UNIDENTIFIED_DESC @202
	LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END									// Removes irrelevant vanilla ARROW projectile if needed.
	LPF ALTER_SPELL_EFFECT INT_VAR silent = 1 check_globals = 0 duration_high = 10 END	// Fixes duration if needed.
BUT_ONLY

// Innate Chaotic Commands
COPY_EXISTING ~SPPR508.SPL~		~override/gwshur2.spl~ 
	READ_LONG  0x64 abil_off ELSE 0
	READ_ASCII (abil_off + 0x04) "bam" (8)										// reads the bam filename from ability
	WRITE_LONG 0x18 THIS | BIT14 | BIT25										// adds Non-magical ability (Ignore dead/wild magic) & Castable when silenced
	WRITE_SHORT  0x1c 4															// sets spell type to innate (4)
	WRITE_LONG   0x34 1															// sets spell level to 1 to avoid scripting issues
	WRITE_ASCIIE 0x3a "%bam%" #8												// writes the bam filename from abilities to spell icon
	FOR (level = 2 ; level <= 50 ; ++level) BEGIN
		LPF DELETE_SPELL_HEADER INT_VAR min_level = level header_type = 1 END	// delete all headers past level 1
	END
	LPF ALTER_SPELL_HEADER INT_VAR location = 4 target = 5 projectile = (IDS_OF_SYMBOL (projectl SPARKLBL) + 1) END							// if Fixpack does not provide this fix
	LPF DELETE_EFFECT INT_VAR check_globals = 0 header = 0 header_type = 1 multi_match = 1 match_opcode = 267 match_parameter1 = 16233 END	// if Fixpack does not provide this fix
	LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 resist_dispel = 0 END																// innate ability : not dispellable.
	LPF ALTER_SPELL_EFFECT INT_VAR duration_high = 60 END																					// sets duration to 60 seconds
BUT_ONLY

// THAC0 +1
COPY ~%MOD_FOLDER%/spells/gwshur3.spl~	~override~


/* ========================================== *
 *             Items Restrictions             *
 * ========================================== */

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
//	TO_LOWER %SOURCE_DEST%
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN			// Movable, otherwise useless.
			GW_weapon_unusable = 0
			GW_armor_unusable = 0
			READ_SHORT 0x1c category
			READ_ASCII 0x22 animation (2)
			READ_BYTE  0x31 proficiency
			// 90 longsword - 91 shortsword - 103 crossbow - 107 sling
			PATCH_MATCH proficiency WITH
				90 91 103 107 BEGIN
					SET GW_weapon_unusable = 1
				END
				DEFAULT
			END
			// Leather armors = ~Shuri Ninja may not wear heavier armor than leather~ AND ~Shuri Ninja may not wear any armor~
			PATCH_IF ("%animation%" STRING_EQUAL_CASE "2A") AND (GW_weapon_unusable = 0) AND (GW_shuri_kit > 1) BEGIN
				GW_armor_unusable = 0
				PATCH_MATCH ~%GW_shuri_kit%~ WITH
					3 BEGIN	SET GW_armor_unusable = 1 END			// ~Shuri Ninja Kit may not wear any armor~
					2 BEGIN LPF ~GW_CHECK_LEATHER~ RET GW_armor_unusable = GW_unusable END END
					DEFAULT
				END
			END
			// Shields = Shuri Ninja may not equip shields
			PATCH_IF (category = 12) AND (GW_armor_unusable = 0) AND (GW_weapon_unusable = 0) AND (GW_shuri_kit > 1) BEGIN
				READ_BYTE 0x20 kit_usability_thief
				PATCH_IF ((kit_usability_thief & BIT6) = 0) BEGIN	// if usable by thief ((kit_usability_thief BAND 0b01000000) == 0b00000000) - kit_usability_thief BAND `BIT6
					SET GW_armor_unusable = 1
//					PATCH_PRINT "CONTROL DEBUGGING %SOURCE_RES%"
				END
			END
			PATCH_IF (GW_armor_unusable OR GW_weapon_unusable) AND (GW_shuri_kit > 1) BEGIN
				LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~SHURI~) parameter2 = 9 special = RESOLVE_STR_REF (@102) END
			END
				
		END	// of PATCH_IF ((flags1 & BIT3) = BIT3)
	END		// of PATCH_IF (SOURCE_SIZE > 0x71)
BUT_ONLY
